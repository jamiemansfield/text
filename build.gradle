plugins {
    id 'java'
    id 'maven'

    id 'net.minecrell.licenser' version '0.3'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'me.jamiemansfield.mc'
archivesBaseName = project.name.toLowerCase()
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:21.0'
    compile 'com.google.code.gson:gson:2.8.0'
}

license {
    header = file('HEADER.txt')
}


task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = 'https://github.com/jamiemansfield/text'

                    scm {
                        url = 'https://github.com/jamiemansfield/text'
                        connection = 'scm:git:https://github.com/jamiemansfield/text.git'
                        developerConnection = 'scm:git:git@github.com:jamiemansfield/text.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/jamiemansfield/text/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
